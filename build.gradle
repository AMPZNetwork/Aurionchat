plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT' apply false
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        api 'com.google.code.gson:gson:2.8.5'
        api 'com.rabbitmq:amqp-client:5.14.2'
        compileOnly 'net.luckperms:api:5.4'
        compileOnly 'org.apache.logging.log4j:log4j-api:2.19.0'
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
        }
    }

    group = project.pluginGroup
    version = project.pluginVersion

    build.dependsOn shadowJar

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    jar{
        archivesBaseName = project.jarName + "-" + project.name
    }

    shadowJar {
        minimize()
        dependencies {
            include(dependency('com.mineaurion.aurionchat:.*'))
            include(project(':common'))
            include(dependency('com.rabbitmq:amqp-client:.*'))
            include(dependency {
                it.moduleGroup == 'org.slf4j'
            })
            exclude '/mappings/*'
        }
        relocate 'com.rabbitmq', 'com.mineaurion.aurionchat.lib.rabbitmq'
        relocate 'org.slf4j', 'com.mineaurion.aurionchat.lib.slf4j'
        relocate 'net.kyori.adventure', 'com.mineaurion.aurioneconomy.lib.adventure'
        relocate 'net.kyori.event', 'com.mineaurion.aurioneconomy.lib.eventbus'
    }
}
