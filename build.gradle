buildscript {
    repositories {
        mavenCentral()
        maven { url  = 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        api 'com.google.code.gson:gson:2.8.5'
        api 'com.rabbitmq:amqp-client:5.14.2'
        compileOnly 'net.luckperms:api:5.4'
        compileOnly 'org.apache.logging.log4j:log4j-api:2.19.0'
    }

    compileJava.options.encoding = 'UTF-8'
    group = project.pluginGroup
    version = project.pluginVersion

    build.dependsOn shadowJar

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }


    jar{
        archivesBaseName = project.jarName + "-" + project.name
    }
    shadowJar {
        minimize()
        dependencies {
            include(dependency('com.mineaurion.aurionchat:.*'))
            include(project(':common'))
            include(dependency('com.rabbitmq:amqp-client:.*'))
            include(dependency {
                it.moduleGroup == 'org.slf4j'
            })
        }
        relocate 'com.rabbitmq', 'com.mineaurion.aurionchat.lib.rabbitmq'
        relocate 'org.slf4j', 'com.mineaurion.aurionchat.lib.slf4j'
    }
}
